# SDP: last modification date 2/24/2011
#umask 022

# Only execute this block if this an interactive shell (behave like .login)
if ( $?prompt ) then


setenv PAGER	/usr/bin/less
setenv EDITOR	/usr/bin/vi
setenv VISUAL	/usr/bin/vi

# SHELL-RELATED ALIASES
alias a		alias
a u			unalias
a h			history
a j			jobs
a d			dirs -v
a po		popd
a pu		pushd
a rot		pushd +1

# host specific commands
# OIA
# set web = /srv/www/htdocs/oiaweb
# set cdpath = ( / ~ /srv/www/htdocs/oiaweb/ )
# a oia	ssh -l root 140.254.17.250
# a gphp	'grep \!* *.php */*.php'

# if ( $path matches '~/bin')
set path = ( $path ~/bin )
# endif


# prompt before rm *
set rmstar on

a sv		'vi +"\!{:1}:" \!:2*'
a lsd		'ls -F'
a ll		'ls -l'
a lr		'ls -ltr'
a dots		'ls -d .??*'
a lsdir		'ls -l | grep drw'
a cx		"chmod +x"
a mcd		"mkdir \!:1; cd \!:1;"
a ..		"cd .."
a .			"source"
a c			'clear'
a orig 		'cp \!* \!*.orig'
a day		"date '+%a %h %d %Y'"
a src		'source ~/.tcshrc'
a bye		exit
a logout	exit
a lo		exit
a procs		"ps -elf | cut -c76- | sort | crk '/\d' | uniq -c | sort -n"
a pid		"ps axu  | grep \!* | grep -v grep"
a pidw		"ps axuww| grep \!* | grep -v grep"
a slay		'(set j=`ps -ax|grep \!*|head -1`;kill -9  $j[1]) >& /dev/null'
a zap		'(set j=`ps -ax|grep \!*|head -1`;kill -1  $j[1]) >& /dev/null'
a term		'(set j=`ps -ax|grep \!*|head -1`;kill -15 $j[1]) >& /dev/null'
a rp		'set path=($path)'
a prepath	'set path=(\!* $path)'
a postpath	'set path=($path \!*)'
a addpath	'set path=($path \!*)'
a here		'set path=($path .)'
a pwdpath	'set path=($path `pwd`)'
a ssh		'ssh -XY'
a ifc		'ifconfig -a'
a str		'strings - \!* | less'
a vrc		'vi ~/.tcshrc'
a vvrc		'vi ~/.vimrc'

if ( $uid == 0 ) then
  set prompt = ": %S%m%s@%.03 [%h]; "
else
  set prompt = "%S%m%s@%c03 <%h> "
endif
set history=2048 savehist=(2048 merge)
# set histdup='prev'
# set histdup='all'
set histdup='erase'
set autolist=ambiguous          # autolist on ambiguous
set histdup=prev                # don't push dup entries oh history
set echo_style=both             # honor both SYSV and BSD style echo conventions
set fignore='.o'                # suffixes to ignore during filename completion
set showdots='-A'               # show dot files in completions
if ( -f ~/.completion) then
  source ~/.completion
endif
if ( -f ~/.manta) then
  source ~/.manta
endif
endif
