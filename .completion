# SDP last modification date 6/12/2012
alias com	complete

# directories
com cd          'p/1/d/'
com pu          'p/1/d/'
com pushd       'p/1/d/'
com rmdir       'p/1/d/'

# signal names
# also note that the initial - can be created with the first completion
# but without appending a space (note the extra slash with no
# append character specified)
comp kill       'c/-/S/' 'p/1/(-)//'
# com kill        "p/1/S/" "n/`ps -elf   | perl -lane 'print [2];'`/"

# aliases
com alias       'p/1/a/'
com unalias     'n/*/a/'

# commands
com which       'p/1/c/'
com where       'p/1/c/'
com man         'n/*/c/'

# variables
com set         'c/*=/f/' 'p/1/s/=' 'n/=/f/'
# com set         'p/1/s/'
com unset       'p/1/s/'

# environment variables
com setenv      'p/1/e/'
com unsetenv    'p/1/e/'
# com talk        "p/1/u/"
# com elm         "c@=@F:$HOME/Mail/@"
# com mutt        "c@=@F:$HOME/Mail/@"

# limits
com limit       'p/1/l/'

# key bindings
com bindkey     'C/*/b/'

# groups
com chgrp       'p/1/g/'

# users
com chown       'p/1/u/'

# You can use complete to provide extensive help for complex commands
# like find.  
# Please check your version before using these completions, as some
# differences may exist.
com find        'n/-name/f/' 'n/-newer/f/' 'n/-{,n}cpio/f/' 'n/-user/u/' \
                'n/-fstype/(nfs 4.2)/' 'n/-type/(b c d f | p s)/' \
                'c/-/(name newer cpio ncpio exec ok user group fstype type \
                atime ctime mtime depth inum ls nogroup nouser perm print \
                prune size xdev mount follow a o local)/' \
                'n/-exec/c/{} \\;' 'n/-ok/c/{} \\;' 'n/-group/g/' \
                'n/*/d/'

# set up cc to complete only with files ending in .c, .a, and .o
com cc          'p/*/f:*.[cao]/'

# of course, this completes with all current completions
com uncomplete  'p/*/X/'

# complex completion for ln
# In all cases, if you start typing, it completes with a filename
# But if you complete without typing anything you get this:
#   first argument:           adds "-s"
#   arguments that follow -s: reminds you of which argument is expected
complete ln     'C/?/f/' 'p/1/(-s)/' 'n/-s/x:[first arg is path to original file]/' 'N/-s/x:[second arg is new link]/'

# set a list of hosts, for use with rlogin
set hostlist=(foo bar baz)
# complete rlogin 'p/1/$hostlist/'
# complete rsh 'p/1/$hostlist/' 'p/2/c/'
com ssh         'p/1/$hostlist/' 'p/2/c/'

# rcp and scp allow arguments to be references to either local or remote
# files.  It's impossible to complete remote files, but its useful to assume
# that the remote file structure is similar to the local one.
#
# when you first start typing, it could be any of a username, hostname,
# or filename.  But filename is probably the most useful case, so:
#
# complete arguments as regular filenames, with following exceptions
# if "@" has been typed, complete with a hostname, and append a colon (:)
# if ":" has been typed, complete with a filename relative to home directory
# if ":/" has been typed, complete with a filename relative to root directory
# 
com scp         "c,*:/,F:/," "c,*:,F:$HOME," 'c/*@/$hostlist/:/'
com sftp        "c,*:/,F:/," "c,*:,F:$HOME," 'c/*@/$hostlist/:/'



com tar			c/-[Acru]*/"(b B C f F g G h i l L M N o P R S T v V w W X z Z)"/ \
		c/-[dtx]*/"( B C f F g G i k K m M O p P R s S T v w x X z Z)"/ \
		p/1/"(A c d r t u x -A -c -d -r -t -u -x --catenate --concatenate --create --diff --compare --delete --append --list --update --extract --get --help --version)"/ \
		c/--/"(catenate concatenate create diff compare delete append list update extract get atime-preserve block-size read-full-blocks directory checkpoint file \
		force-local info-script new-volume-script incremental listed-incremental dereference ignore-zeros ignore-failed-read keep-old-files starting-file \
		one-file-system tape-length modification-time multi-volume after-date newer old-archive portability to-stdout same-permissions preserve-permissions \
		absolute-paths preserve record-number remove-files same-order preserve-order same-owner sparse files-from null totals verbose label version \
		interactive confirmation verify exclude exclude-from compress uncompress gzip ungzip use-compress-program block-compress help version)"/ \
		c/-/"(b B C f F g G h i k K l L m M N o O p P R s S T v V w W X z Z 0 1 2 3 4 5 6 7 -)"/ \
		C@[/dev]@f@ \
		n/-c*f/x:'<new_tar_file, device_file, or "-">'/ \
		n/{-[Adrtux]*f,--file}/f:*.{tar,taz,tgz}/ \
		N/{-x*f,--file}/'`tar -tf $:-1`'/ \
		n/--use-compress-program/c/ \
		n/{-b,--block-size}/x:'<block_size>'/ \
		n/{-V,--label}/x:'<volume_label>'/ \
		n/{-N,--{after-date,newer}}/x:'<date>'/ \
		n/{-L,--tape-length}/x:'<tape_length_in_kB>'/ \
		n/{-C,--directory}/d/ \
		N/{-C,--directory}/'`\ls $:-1`'/ \
		n/-[0-7]/"(l m h)"/
alias com
